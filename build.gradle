plugins {
	id "java"
}

allprojects {
	apply plugin: "java-library"

	repositories {
		mavenCentral()
		maven { url = "https://oss.sonatype.org/content/repositories/snapshots/" }
		maven { url = "https://jitpack.io" }
	}

	java {
		sourceCompatibility = "17"
		targetCompatibility = "17"
	}

	project.ext.boilerVersion = "3b425379ff5cd96f0527ed7ad6a79827e831e42b"
	project.ext.jUnitVersion = "5.10.3"
	project.ext.allNatives = [
			"natives-windows", "natives-windows-x86", "natives-windows-arm64",
			"natives-linux", "natives-linux-arm32", "natives-linux-arm64",
			"natives-freebsd", "natives-linux-ppc64le", "natives-linux-riscv64",
			"natives-macos", "natives-macos-arm64"
	]

	dependencies {
		implementation platform("org.lwjgl:lwjgl-bom:3.3.4")

		testImplementation platform("org.junit:junit-bom:$jUnitVersion")
		testImplementation "org.junit.jupiter:junit-jupiter:$jUnitVersion"
		testRuntimeOnly "org.junit.platform:junit-platform-launcher"
	}

	compileTestJava.options.encoding = 'UTF-8'

	test {
		useJUnitPlatform()
	}
}

project(":core") {
	dependencies {
		compileOnly "org.lwjgl:lwjgl"
		compileOnly "org.lwjgl:lwjgl-freetype"
		compileOnly "org.lwjgl:lwjgl-harfbuzz"

		implementation "com.github.knokko:memory-query:v1.1" // TODO Remove after testing

		testImplementation project(":core-bundle")
		testImplementation project(":unicode-fonts")
		testImplementation project(":test-helper")
	}
}

project(":core-bundle") {
	dependencies {
		api "org.lwjgl:lwjgl"
		api "org.lwjgl:lwjgl-freetype"
		api "org.lwjgl:lwjgl-harfbuzz"
		api project(":core")

		for (String target : project.ext.allNatives) {
			runtimeOnly "org.lwjgl:lwjgl::$target"
			runtimeOnly "org.lwjgl:lwjgl-freetype::$target"
		}
	}
}

project(":unicode-fonts") {
	dependencies {
		compileOnly project(":core")
	}
}

project(":vulkan") {
	dependencies {
		compileOnly project(":core")
		compileOnly "org.lwjgl:lwjgl-vulkan"
		compileOnly "org.lwjgl:lwjgl-vma"
		compileOnly "com.github.knokko:vk-boiler:$boilerVersion"

		testImplementation project(":vulkan-bundle")
		testImplementation project(":unicode-fonts")
		testImplementation project(":test-helper")
	}
}

project(":vulkan-bundle") {
	dependencies {
		api project(":core-bundle")
		api project(":vulkan")
		api "org.lwjgl:lwjgl-vulkan"
		api "org.lwjgl:lwjgl-vma"
		api "com.github.knokko:vk-boiler:$boilerVersion"
		runtimeOnly "org.lwjgl:lwjgl-vulkan::natives-macos"
		runtimeOnly "org.lwjgl:lwjgl-vulkan::natives-macos-arm64"
		for (String target : project.ext.allNatives) {
			runtimeOnly "org.lwjgl:lwjgl-vma::$target"
		}
	}
}

project(":sample-unicode-renderer") {
	dependencies {
		implementation project(":vulkan-bundle")
		implementation project(":unicode-fonts")
		implementation project(":test-helper")
		implementation "org.lwjgl:lwjgl-glfw"
		for (String target : project.ext.allNatives) {
			runtimeOnly "org.lwjgl:lwjgl-glfw::$target"
		}
		implementation "com.github.knokko:sample-profiler:v1.0.0"
	}
}

project(":test-helper") {
	dependencies {
		implementation platform("org.junit:junit-bom:$jUnitVersion")
		implementation "org.junit.jupiter:junit-jupiter:$jUnitVersion"
		runtimeOnly "org.junit.platform:junit-platform-launcher"
	}
}
