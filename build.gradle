plugins {
	id "java"
}

allprojects {
	apply plugin: "java"

	repositories {
		mavenCentral()
		maven { url = "https://oss.sonatype.org/content/repositories/snapshots/" }
	}

	java {
		sourceCompatibility = "17"
		targetCompatibility = "17"
	}

	project.ext.jUnitVersion = "5.10.3"
	project.ext.allNatives = [
			"natives-windows", "natives-windows-x86", "natives-windows-arm64",
			"natives-linux", "natives-linux-arm32", "natives-linux-arm64",
			"natives-freebsd", "natives-linux-ppc64le", "natives-linux-riscv64",
			"natives-macos", "natives-macos-arm64"
	]

	dependencies {
		implementation platform("org.lwjgl:lwjgl-bom:3.3.4")
		compileOnly "org.lwjgl:lwjgl"
		compileOnly "org.lwjgl:lwjgl-freetype"
		compileOnly "org.lwjgl:lwjgl-harfbuzz"

		testImplementation "org.lwjgl:lwjgl"
		testImplementation "org.lwjgl:lwjgl-freetype"
		testImplementation "org.lwjgl:lwjgl-harfbuzz"

		for (String target : project.ext.allNatives) {
			testRuntimeOnly "org.lwjgl:lwjgl::$target"
			testRuntimeOnly "org.lwjgl:lwjgl-freetype::$target"
		}

		testImplementation platform("org.junit:junit-bom:$jUnitVersion")
		testImplementation "org.junit.jupiter:junit-jupiter:$jUnitVersion"
		testRuntimeOnly "org.junit.platform:junit-platform-launcher"
	}

	compileTestJava.options.encoding = 'UTF-8'

	test {
		useJUnitPlatform()
	}
}

project(":core") {
	dependencies {
		testImplementation project(":unicode-fonts")
	}
}

project(":vulkan") {
	repositories {
		maven { url = "https://jitpack.io" }
	}

	project.ext.boilerVersion = "v3.2.1"

	dependencies {
		compileOnly "org.lwjgl:lwjgl-vulkan"
		compileOnly "com.github.knokko:vk-boiler:$boilerVersion"

		testImplementation "org.lwjgl:lwjgl-vulkan"
		testImplementation "com.github.knokko:vk-boiler:$boilerVersion"
		testRuntimeOnly "org.lwjgl:lwjgl-vulkan::natives-macos"
		testRuntimeOnly "org.lwjgl:lwjgl-vulkan::natives-macos-arm64"
	}
}
